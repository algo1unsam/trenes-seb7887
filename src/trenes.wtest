import trenes.*
import locomotoras.*
import vagones.*
import ciudades.*
import depositos.*

describe "Trenes" {
  const vagonCarga = new VagonCarga()
  
  var vagones = [
    new VagonPasajeros(10, 2),
    new VagonPasajeros(10, 3)
  ]
  
  var locomotoras = [
    new Locomotora(1000, 12000, 80),
    new Locomotora(2000, 11000, 40)
  ]
  
  method crearTren() {
    vagonCarga.cargaMaxima(100)
    vagones.add(vagonCarga)
    return new Tren(locomotoras, vagones)
  }
  
  method crearTrenSinEmpuje() {
    return new Tren(new Locomotora(2000, 11000, 40), vagones)
  }
  
  method crearTrenCortaDistancia() {
    return new TrenCortaDistancia(locomotoras, vagones)
  }
  
  method crearTrenLargaDistancia() {
    return new TrenLargaDistancia(locomotoras, vagones)
  }
  
  test "VagonPasajeros: Si ancho es 2m y largo 10m, cantidad de pasajeros es 80" {
    var vagon = new VagonPasajeros(10, 2)
    assert.equals(80, vagon.cantidadDePasajeros())
  }
  
  test "VagonPasajeros: Si ancho es 3m y largo 10m, cantidad de pasajeros es 100" {
    var vagon = new VagonPasajeros(10, 3)
    assert.equals(100, vagon.cantidadDePasajeros())
  }
  
  test "VagonPasajeros: Si puede llevar 80 pasajeros, su peso maximo es 6400" {
    var vagon = new VagonPasajeros(10, 2)
    assert.equals(6400, vagon.pesoMaximo())
  }
  
  test "VagonCarga: Devuelve correctamente su carga maxima" {
    var vagon = new VagonCarga()
    vagon.cargaMaxima(100)
    assert.equals(100, vagon.cargaMaxima())
  }
  
  test "VagonCarga: Si su carga maxima es 100, su peso maximo es 260" {
    var vagon = new VagonCarga()
    vagon.cargaMaxima(100)
    assert.equals(260, vagon.pesoMaximo())
  }
  
  test "Locomotora: Si peso es 1000, peso maximo 12000 y velocidad maxima 80, arrastre es 11000" {
    var locomotora = new Locomotora(1000, 12000, 80)
    assert.equals(11000, locomotora.cuantoPuedeArrastrar())
  }
  
  test "Tren: cantidad de pasajeros total correctamente devuelta" {
    var tren = self.crearTren()
    assert.equals(180, tren.cantidadDePasajeros())
  }
  
  test "Tren: cantidad de vagones livianos correctamente devuelta" {
    var tren = self.crearTren()
    assert.equals(2, tren.cantidadDeVagonesLivianos())
  }
  
  test "Tren: velocidad maxima correctamente devuelta" {
    var tren = self.crearTren()
    assert.equals(40, tren.velocidadMaxima())
  }
  
  test "Tren: es eficiente correctamente devuelto" {
    var tren = self.crearTren()
    assert.that(!tren.esEficiente())
  }
  
  test "Tren: cuantoEmpujeFalta correctamente devuelto" {
    var tren = self.crearTren()
    assert.equals(0, tren.cuantoEmpujeFalta())
  }
  
  test "TrenCortaDistancia: no esta correctamente armada" {
    var tren = self.crearTrenCortaDistancia()
    assert.that(!tren.estaBienArmada())
  }
  
  test "TrenLargaDistancia: no esta correctamente armada" {
    var tren = self.crearTrenLargaDistancia()
    assert.that(!tren.estaBienArmada())
  }
  
  test "TrenCortaDistancia: su velocidad maxima no puede ser mayor a 60" {
    var tren = self.crearTrenCortaDistancia()
    assert.equals(40, tren.velocidadMaxima())
  }
  
  test "TrenLargaDistancia: su velocidad maxima no puede ser mas de 200 si son dos ciudades grandes" {
    var tren = self.crearTrenLargaDistancia()
    tren.origen(new Ciudad(true))
    tren.destino(new Ciudad(true))
    assert.equals(40, tren.velocidadMaxima())
  }
  
  test "Deposito: vagonesMasPesados correctamente devuelto" {
    var deposito = new Deposito()
    var vagon = new VagonPasajeros(10, 3)
    deposito.agregarFormacion(self.crearTren())
    assert.equals([vagon], deposito.vagonesMasPesados())
  }
  
  test "Deposito: necesitaConductorExperimentado correctamente devuelto" {
    var deposito = new Deposito()
    deposito.agregarFormacion(self.crearTren())
    assert.that(deposito.necesitaConductorExperimentado())
  }
  
  test "Deposito: agregarLocomotoraATren no agrega nada si no hace falta" {
    var deposito = new Deposito()
    deposito.agregarFormacion(self.crearTren())
    deposito.agregarLocomotora(new Locomotora(1500, 20000, 85))
    deposito.agregarLocomotoraATren()
    assert.equals(1, deposito.locomotoras().size())
  }
  
  test "Deposito: agregarLocomotoraATren agrega correctamente a formacion que le falta empuje" {
    var deposito = new Deposito()
    deposito.agregarFormacion(self.crearTrenSinEmpuje())
    deposito.agregarLocomotora(new Locomotora(1500, 20000, 85))
    deposito.agregarLocomotoraATren()
    assert.equals(0, deposito.locomotoras().size())
  }
  
}